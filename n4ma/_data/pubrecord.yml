---
- name: Leap group leaders
  md: |
    Group leaders for whom a record in could be identified in Wikidata. New records
    can be added by anyone and tools exist to ease the process.
  rq: |
    #title:Leap group leaders
    SELECT * WHERE {
      VALUES ?leapauthor {
          wd:Q59830751
          wd:Q59679079
          wd:Q59674360
          wd:Q5107972
          wd:Q39049575
          wd:Q30004330
          wd:Q55101059
          wd:Q88590244
          wd:Q88797604
          wd:Q5638525
          wd:Q96210212
          wd:Q49516549
          }

      ?leapauthor rdfs:label ?leapauthorLabel .
      BIND(uri(CONCAT("https://scholia.toolforge.org/author/",REPLACE(str(?leapauthor), "http://www.wikidata.org/entity/", ""))) as ?scholia)

      FILTER (lang(?leapauthorLabel)="en")
    }

- name: Group leaders and their past and present affiliations
  md: |
    All affiliations of known group leaders, including past and present. With a Wikidata update, it will
    be possible to also query if the affiliation is current or not.
  rq: |
    #title: Group leaders and their past and present affiliations
    PREFIX wd: <http://www.wikidata.org/entity/>
    PREFIX wdt: <http://www.wikidata.org/prop/direct/>
    PREFIX wikibase: <http://wikiba.se/ontology#>
    PREFIX bd:<http://www.bigdata.com/rdf#>

    SELECT DISTINCT ?leapauthor ?leapauthorLabel ?scholia_author ?affiliation ?affiliationLabel ?scholia_affiliation WHERE {
      VALUES ?leapauthor {wd:Q59830751 wd:Q59679079 wd:Q59679079 wd:Q59674360 wd:Q5107972 wd:Q39049575 wd:Q30004330 wd:Q55101059 wd:Q88590244 wd:Q88797604 wd:Q5638525 wd:Q96210212 wd:Q49516549}

      ?leapauthor wdt:P108 ?affiliation .
      BIND(uri(CONCAT("https://scholia.toolforge.org/author/",REPLACE(str(?leapauthor), "http://www.wikidata.org/entity/", ""))) as ?scholia_author)
      BIND(uri(CONCAT("https://scholia.toolforge.org/organization/",REPLACE(str(?affiliation), "http://www.wikidata.org/entity/", ""))) as ?scholia_affiliation)
      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
    }
    ORDER BY ?leapauthor

- name: Group leaders and their publication records
  md: |
    The following query lists the publication records of the known group
    leaders. Since the list is quite extensive a different visualization is
    applied. The Wikidata Query Service features various visualizations for the
    results. In this query the Tree visualization is used.
  rq: |
    #title:Group leaders and their publication records
    #defaultView:Tree

    PREFIX wd: <http://www.wikidata.org/entity/>
    PREFIX wdt: <http://www.wikidata.org/prop/direct/>
    PREFIX wikibase: <http://wikiba.se/ontology#>
    PREFIX bd: <http://www.bigdata.com/rdf#>

    SELECT DISTINCT ?leapauthor ?leapauthorLabel ?publication ?publicationLabel ?pmid ?doi  WHERE {
      VALUES ?leapauthor {wd:Q59830751 wd:Q59679079 wd:Q59679079 wd:Q59674360 wd:Q5107972 wd:Q39049575 wd:Q30004330 wd:Q55101059 wd:Q88590244 wd:Q88797604 wd:Q5638525 wd:Q96210212 wd:Q49516549}

      ?publication wdt:P50 ?leapauthor .

      OPTIONAL {?publication wdt:P698 ?pmid .}
      OPTIONAL {?publication wdt:P356 ?doi .}

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
    }


- name: Publications and their topics
  md: |
    The following query lists publications and their topics. Since the list is
    quite extensive, a BubbleChart is applied. The size of the bubbles is
    determined by the number of publications linked to specific topic.
  rq: |
    #title:Publications and their topics
    #defaultView:BubbleChart

    PREFIX wd: <http://www.wikidata.org/entity/>
    PREFIX wdt: <http://www.wikidata.org/prop/direct/>
    PREFIX wikibase: <http://wikiba.se/ontology#>
    PREFIX bd: <http://www.bigdata.com/rdf#>

    SELECT DISTINCT ?topic ?topicLabel (COUNT(?leapauthor) AS ?counts)  WHERE {
      VALUES ?leapauthor {wd:Q59830751 wd:Q59679079 wd:Q59679079 wd:Q59674360 wd:Q5107972 wd:Q39049575 wd:Q30004330 wd:Q55101059 wd:Q88590244 wd:Q88797604 wd:Q5638525 wd:Q96210212 wd:Q49516549}

      ?publication wdt:P50 ?leapauthor ;
                  wdt:P921 ?topic .

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
    }
    GROUP BY ?topic ?topicLabel
    ORDER BY desc(?counts)

- name: Author name strings and their publications
  md: |
    Sometimes an author exists in Wikidata only by its name string. When this
    is the case, authors are not linked to their publication. The author names
    need to be linked to their respective identifiers. The author disambiguator
    can be used to link the author name string to the author identifier.
  rq: |
    #title:Author name strings and their publications
    #description:Finds publications by author name strings

    PREFIX wd: <http://www.wikidata.org/entity/>
    PREFIX wdt: <http://www.wikidata.org/prop/direct/>
    PREFIX wikibase: <http://wikiba.se/ontology#>
    PREFIX bd: <http://www.bigdata.com/rdf#>

    SELECT DISTINCT ?leapauthor ?publication ?publicationLabel ?pmid ?doi ?author_disambiguator WHERE {
      VALUES ?leapauthor { "Adrie Jc Steyn"
                            "Assaf Zaritsky"
                            "Michael Roukes"
                            "Chris Sander"
                            "Fabian J. Theis"
                            "Maddy Parsons"
                            "Gregory Hannon"
                            "Virginie Rozot"
                            "Denise Kirschner"
                            "Hagan Bayley"
                            "Stéphane Pagès"
                            "Joerg Bewersdorf"}

      ?publication wdt:P2093 ?leapauthor .
      BIND(uri(CONCAT("https://author-disambiguator.toolforge.org/names_oauth.php?doit=Look+for+author&name=",str(?leapauthor))) as ?author_disambiguator)

      SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
      OPTIONAL {?publication wdt:P698 ?pmid .}
      OPTIONAL {?publication wdt:P356 ?doi .}
    }
